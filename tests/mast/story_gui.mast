import tests/mast/bar.mast
~~~~~~~~~~~~~~~~
# Example of embedded python script
@export()
def test_func(a,b):
    return a + b

@export()
class MyShip(Npc):
    pass

free = 99
export_var("free", free)
export_var("free_shared", free, True)
~~~~~~~~~~~~~~~~

shared alisa = "ter #6d8b01 3 0;ter #6d8b01 4 1;ter #6d8b01 4 3;ter #CC9966 8 4 6 -2;ter #fff 13 0 14 -2;ter #fff 3 6;"
shared frank = "ter #f2efee 0 0;ter #f2efee 2 0;ter #f2efee 0 3;ter #fff 12 1 14 -2;ter #fff 2 6;ter #3d0463 9 0 12 4;"
shared mission_started = False

# Non-Shared data unique to the client (runner)
learn = True


->Briefing

================== Briefing ================

if IS_SERVER:
    area 21 10 60 90
else:
    area 30 10 60 90
end_if



face alisa
"""""""""""""""""""""""""""""""""
Welcome to the TSN {test_func(2,5)} {free}
I'm Captain Alisa. I've been running things in this sector for 10 years. We run a tight sector.
I don't take any crap and I do not like people who lie and disobey orders out of a matter of principle.
"""""""""""""""""""""""""""""""""
row

"""""""""""""""""""""""""""""""""
Hi, I'm Captain frank. Alisa isn't that bad once you get to know her.

You're in for an adventure

Have you ever flown a spaceship?
"""""""""""""""""""""""""""""""""

face frank


section
area 60 10 100 90

blank
row
ship Battle Cruiser
row
blank
"""""""""""""""""""""""""""""""""
This is your first ship
"""""""""""""""""""""""""""""""""
blank

-------------await choice:---------------------
    + "Yes.." if learn == True:
        -> YouLied 
    + "Yes.." if learn == False:
        -> YouTrained 
    + "Ready to learn":
         -> Learn
    + "Bar":
     ->> GotoBar
    + "HUD":
    ->> Hud
    + "Exit": 
    ->END
-----------------end_await------------------

# This is for the bar/hud when they pops
->Briefing

=========== Test ============

""" {self} """
->END


=================== YouLied ========

area 20 10 100 90
face alisa

""""""""""""""""""""""""""""""""""""""""""""""
I will not tolerate those who are untruthful.

You should board the next shuttle and get the hell out of my sector
"""""""""""""""""""""""""""""""""""""""""""

await choice:
+ "Exit":
->END
end_await


============== YouTrained =========

area 20 10 100 90

face alisa

"""""""""""""""""""""""""""""""""
Congratulations!

Your training is complete

You're ready to command your own ship.

Good luck
"""""""""""""""""""""""""""""""""

await choice:
    + "Resume Mission"  if mission_started==True:
        -> ResumeMission
    + "Start Mission"  if mission_started==False:
     -> StartMission
end_await

================= StartMission ==============================

~~sbs.create_new_sim()~~

shared mission_started = True
->>MapStuff
->ResumeMission

================= ResumeMission ==============================
~~ sbs.resume_sim() ~~
->YouTrained

==================           Learn     ===========

area 20 20 100 40

learn = False

button "Test 1": -> Learn end_button

button "Test 2": ->Learn end_button
button "Test 3": -> Learn  end_button
button "Test 4": ->Learn end_button
row
button "Test 5": -> Learn end_button
button "Test 6": ->Learn end_button
button "Test 7": -> Learn end_button
button "Test 8": ->Learn end_button
row
button "Test 9": -> Briefing end_button
button "Test 10": -> Briefing end_button

slider slider_value 0.0 1.0 0.5
checkbox wilma "Yaba"

await choice:
    + "exit":
    ->END
end_await

====================== NoChoice ======================

->END


====================== MapStuff ======================
shared player = PlayerShip().spawn(sim, 0, 0, 0,"Artemis", "tsn", "Battle Cruiser")
shared tsn =  ~~ [
    Npc().spawn(sim, 1000, 0, 1000, "TSN0", "tsn", "Battle Cruiser", "behav_npcship").py_object,
    Npc().spawn(sim ,1200, 0, 1000, "TSN1", "tsn", "Battle Cruiser", "behav_npcship").py_object,
    Npc().spawn(sim, 1400, 0, 1000, "TSN2", "tsn", "Battle Cruiser", "behav_npcship").py_object
    ]~~

<<-


===================== Hud ============

for y in range(4):
for x in range(4):
    f = "hud #fff {x} {y}"
    face f
next x
row
next y


await choice:
    + "Back":
        <<-
end_await

